#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_mysteeef_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${_prompt_mysteeef_colors[2]}%b%f%u%c%F{231}●%f"
  else
    branch_format="${_prompt_mysteeef_colors[2]}%b%f%u%c"
  fi

  zstyle ':vcs_info:*:prompt:*' formats " %F{231}‹%f ${branch_format} %F{231}›%f"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi

  print -P "\n%{$(mysteeef_ssh_connection)$(mysteeef_user_prompt)"'${vcs_info_msg_0_}'""'$python_info[virtualenv]'" : %F{white}%~%f%}"
}

function mysteeef_ssh_connection() {
  if [[ -n $SSH_CONNECTION ]]; then
    echo "${_prompt_mysteeef_colors[4]}(ssh)%f "
  fi
}

function mysteeef_user_prompt() {
  if [[ $UID == 0 || $EUID == 0 ]]; then
    echo "${_prompt_mysteeef_colors[4]}%m%f"
  else
    echo "${_prompt_mysteeef_colors[5]}%n@%m%f"
  fi
}

function prompt_mysteeef_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_mysteeef_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_mysteeef_colors=(
      "%F{33}"
      "%F{226}"
      "%F{192}"
      "%F{196}"
      "%F{82}"
    )
  else
    _prompt_mysteeef_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_mysteeef_colors[2]}%b%f%u%c)"
  local action_format="(${_prompt_mysteeef_colors[5]}%a%f)"
  local unstaged_format="${_prompt_mysteeef_colors[4]}●%f"
  local staged_format="${_prompt_mysteeef_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format ' (%v)'


  local ret_status="%(?:${_prompt_mysteeef_colors[5]}:${_prompt_mysteeef_colors[4]})%?%f"

  # Define prompts.
  PROMPT="[${ret_status}] %# "
  RPROMPT=''
}

prompt_mysteeef_setup "$@"
